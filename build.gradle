plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id "legacy-looming" version "1.3-SNAPSHOT"
    id "io.github.juuxel.loom-vineflower" version "1.11.0"
    id "com.modrinth.minotaur" version "2.+"
    id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings legacy.yarn(project.minecraft_version, project.yarn_mappings_build)
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    //modImplementation "net.legacyfabric.legacy-fabric-api:legacy-fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

def ENV = System.getenv()

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "fLCmgUPa"
    versionNumber = "${project.mod_version}"
    versionName = "Phosphor-Legacy v${project.mod_version} (1.12.2)"
    versionType = "release"
    changelog = "Initial release"

    uploadFile = remapJar
    gameVersions = ["1.12.2"]
    loaders = ["fabric"]
}
